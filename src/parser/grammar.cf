-- programs ------------------------------------------------

entrypoints Program ;
Start.   Program ::= [FnDef] ;

-- functions ------------------------------------------------

FnDef.	   FnDef ::= Type Ident "(" [ArgDec] ")" Block ;
separator nonempty FnDef "" ;
ArgDec.    ArgDec ::= Type Ident;
separator  ArgDec "," ;

-- statements ----------------------------------------------

Block.     Block ::= "{" [Stmt] "}" ;
separator  Stmt "" ;
Empty.     Stmt ::= ";" ;
Decl.      Stmt ::= Type [Item] ";" ;
Item.      Item ::= Ident "=" Expr ;
separator nonempty Item "," ;
Ass.       Stmt ::= Ident "=" Expr  ";" ;
Ret.       Stmt ::= "return" Expr ";" ;
Cond.      Stmt ::= "if" "(" Expr ")" Block  ;
CondElse.  Stmt ::= "if" "(" Expr ")" Block "else" Block  ;
While.     Stmt ::= "while" "(" Expr ")" Block ;
Print.     Stmt ::= "print" "(" Expr ")" ";" ;
Println.     Stmt ::= "println" "(" Expr ")" ";" ;
FuncStmt.  Stmt ::= FnDef ;

-- Types ---------------------------------------------------

Int.       Type ::= "int" ;
Str.       Type ::= "string" ;
Bool.      Type ::= "bool" ;
Array.     Type ::= Type "[" Expr "]" ;

-- Expressions ---------------------------------------------

EArr.      Expr6 ::= "[" [Expr] "]" ;
EArrVal.   Expr6 ::= Ident "[" [Expr] "]" ;
EVar.      Expr6 ::= Ident ;
EInt.      Expr6 ::= Integer ;
ETrue.     Expr6 ::= "true" ;
EFalse.    Expr6 ::= "false" ;
EApp.      Expr6 ::= Ident "(" [Arg] ")" ;
EString.   Expr6 ::= String ;
Neg.       Expr5 ::= "-" Expr6 ;
Not.       Expr5 ::= "!" Expr6 ;
EMul.      Expr4 ::= Expr4 MulOp Expr5 ;
EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;
ERel.      Expr2 ::= Expr2 RelOp Expr3 ;
EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;
EOr.       Expr ::= Expr1 "||" Expr ;
coercions  Expr 6 ;
separator  Expr "," ;

ArgVal.    Arg ::= Expr ;
ArgVar.    Arg ::= "var" Ident ;
separator  Arg "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;
Minus.     AddOp ::= "-" ;
Times.     MulOp ::= "*" ;
Div.       MulOp ::= "/" ;
Mod.       MulOp ::= "%" ;
LTH.       RelOp ::= "<" ;
LE.        RelOp ::= "<=" ;
GTH.       RelOp ::= ">" ;
GE.        RelOp ::= ">=" ;
EQU.       RelOp ::= "==" ;
NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;
comment    "/*" "*/" ;
